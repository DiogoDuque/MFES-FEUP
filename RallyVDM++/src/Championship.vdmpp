class Championship is subclass of State

types 
	public String = seq of char;
	public Date = DateUtils`Date;
  public TeamName = Team`TeamName;

instance variables
  private series: seq of Rally := [];
  private currentRallyIndex: int := -1;
  private teams: map TeamName to Team := { |-> };
  private partners: set of Partner := {};
  private name: String;
  private startDate: Date;
  private endDate: Date;
  
  -- nome � chave de rally/team/partner, isto �, n�o podem existir dois rallies/teams/partners diferentes com o mesmo nome
  inv not exists r1, r2 in seq series &
        r1 <> r2 and r1.GetName() = r2.GetName();
  inv not exists p1, p2 in set partners &
        p1 <> p2 and p1.GetName() = p2.GetName();
	inv endDate > startDate;     
  inv forall i in set inds series &
      i > 1 => let s1 = series(i-1), s2 = series(i) in s1.GetStartDate() < s2.GetStartDate();	
	inv forall r in seq series & r.GetEndDate() < endDate and r.GetStartDate() > startDate;

operations
	/**
	 * Instantiates a championship instance.
	 */
  public Championship: String * Date * Date ==> Championship
		Championship(name0, startDate0, endDate0) == (
			  name := name0;
			  startDate := startDate0;
			  endDate := endDate0;
			  return self;
			)
		pre endDate0 > startDate0 and name <> ""
		post name = name0 and startDate = startDate0 and endDate = endDate0 and teams = { |-> };
	
	-- *** Transactions ***

	/**
	 * Starts the championship, initializing the first rally participants
	 */	
	public StartChampionship: () ==> () 
		StartChampionship() == (
			currentRallyIndex := 1;
		  series(currentRallyIndex).StartRally(GetDrivers());	
			Init();
		)
		pre state = <OffSeason> and series <> [] and teams <> { |-> } and not exists s in seq series & s.GetState() <> <OffSeason>
		post state = <Occurring> and currentRallyIndex <> -1 and series(currentRallyIndex).GetState() = <Occurring>;

	/**
	 * Updates the current rally. 
	 * The set of performances is used to define the driver performances of the current stage that is happening
	 */	
	public UpdateCurrentRally: set of Performance ==> ()
		UpdateCurrentRally(performances) == (
			series(currentRallyIndex).NextStage(performances);
		)
		pre state = <Occurring> and series(currentRallyIndex).GetState() = <Occurring>;

	/**
	 * Checks if the current rally is completed
	 * 
	 * @return bool
	 */	
	public IsCurrentRallyCompleted: () ==> bool
		IsCurrentRallyCompleted() == (
			if(series(currentRallyIndex).GetState() = <Completed>)
				then return true
			else return false	
		)
		pre state = <Occurring>;

	/**
	 * Starts the next rally, only if the current one is completed
	 */	
	public NextRally: () ==> () 
		NextRally() == (
			currentRallyIndex := currentRallyIndex + 1;	
		  if(currentRallyIndex > len series) then EndChampionship() else series(currentRallyIndex).StartRally(GetDrivers());	
		)
		pre state = <Occurring> and series(currentRallyIndex).GetState() = <Completed>;

	/**
	 * Ends the championship
	 */	
	private EndChampionship: () ==> () 
		EndChampionship() == (
			currentRallyIndex := -1;	
			Finalize();
		)
		pre state = <Occurring>
		post state = <Completed> and not exists r in seq series & r.GetState() <> <Completed>;

	/**
	 * Adds a partner to the championship
	 */			
	public AddPartner: Partner ==> ()
		AddPartner(p) == (
			partners := partners union {p}
		)
		pre state = <OffSeason> and not exists p1 in set partners & p1.GetName() = p.GetName()
		post p in set partners;

	/**
	 * Gets the points of a team on a specific rally
	 * 
	 * @return int
	 */		
	public GetTeamRallyRanking: String * String ==> int
	 GetTeamRallyRanking(teamName, rallyName) == (
			let i in set inds series be st series(i).GetName() = rallyName in return series(i).GetTeamRanking(teamName);
	 )
	 pre teamName in set dom teams and exists r in seq series & r.GetName() = rallyName;

	/**
	 * Gets the global ranking of a single driver, during the respective championship
	 * 
	 * @return int
	 */			 
	 pure public GetDriverGlobalRanking: Driver ==> int
	 	GetDriverGlobalRanking(d) == (
	 		dcl points: int := 0;
	 		for rally in series
	 			do
	 				if(rally.GetState() <> <OffSeason>)
	 					then (points := points + rally.GetDriverRanking(d).GetPoints(););
			return points;
 		);

	/**
	 * Gets the global ranking (points) of a team
	 * 
	 * @return int
	 */		 
	public GetTeamGlobalRanking: String ==> int
	 GetTeamGlobalRanking(teamName) == (
	 	dcl points: int := 0;
    for elem in series  
     	do 
        if (elem.GetState() <> <OffSeason>)
            then (points := points + elem.GetTeamRanking(teamName););
		
		return points;
	 )
	 pre teamName in set dom teams; 

	/**
	 * Gets the championship global ranking, with the points of each team
	 * 
	 * @return map String to int
	 */	
	public GetTeamsRanking: () ==> map String to int
	 GetTeamsRanking() == (
	 		dcl rankings: map String to int := { |-> };
	 		for all elem in set dom teams
	 			do 
	 				rankings := rankings munion { elem |-> GetTeamGlobalRanking(elem) };
	 		return rankings;
	 );

	/**
	 * Removes a partner from the championship
	 */		
	public RemovePartner: Partner ==> ()
		RemovePartner(p) == (
			partners := partners \ {p}
		)
		pre state = <OffSeason> and p in set partners
		post partners = partners~ \ {p};	

	/**
	 * Adds a rally to the championship, whose dates must be between the championship start and end
	 * Furthermore, the rally state must be OffSeason
	 */	
	public AddRally: Rally ==> ()
		AddRally(r) == (
			series := series ^ [r]
		)
		pre r.GetStages() <> [] and r.GetStartDate() > startDate and r.GetEndDate() < endDate and state = <OffSeason> and not exists r1 in seq series & r1.GetName() = r.GetName();

	/**
	 * Adds a team to the championship
	 */	
	public AddTeam: Team ==> ()
		AddTeam(t) == (
			teams := teams munion {t.GetName() |-> t}; 
		)
		pre state = <OffSeason> and t.GetName() not in set dom teams
		post teams = teams~ munion {t.GetName() |-> t};

	/**
	 * Removes a team from the championship
	 */	
	public RemoveTeam: Team ==> ()
		RemoveTeam(t) == (
			teams := {t.GetName()} <-: teams;
		)
		pre state = <OffSeason> and t.GetName() in set dom teams
		post t.GetName() not in set dom teams;
	
	-- *** Getters ***

	/**
	 * Gets the current rally that its occurring
	 * 
	 * @return Rally
	 */	
	pure public GetCurrentRally: () ==> [Rally]
		GetCurrentRally() == (
			return 
				if state = <Occurring> and currentRallyIndex >= 1
           then series(currentRallyIndex)
        else nil;	
		);
	
	/**
	 * Gets the championship set of drivers
	 * 
	 * @return set of Driver
	 */		
	pure public GetDrivers: () ==> set of Driver
		GetDrivers() ==
			return dunion { t.GetDrivers() | t in set rng teams };

	/**
	 * Gets the championship name.
	 * 
	 * @return String
	 */			
	pure public GetName: () ==> String
		GetName() ==
		  return name
		post RESULT = name;

	/**
	 * Gets the championship start date.
	 * 
	 * @return Date
	 */			
	pure public GetStartDate: () ==> Date
		GetStartDate() ==
		  return startDate
		post RESULT = startDate;

	/**
	 * Gets the championship end date.
	 * 
	 * @return Date
	 */			
	pure public GetEndDate: () ==> Date
		GetEndDate() ==
		  return endDate
		post RESULT = endDate;		

	/**
	 * Gets the championship teams
	 * 
	 * @return map TeamName to Team
	 */			
	pure public GetTeams: () ==> map TeamName to Team
	GetTeams() ==
		return teams
	post RESULT = teams;

	/**
	 * Gets the championship partners
	 * 
	 * @return set of Partner
	 */			
	pure public GetPartners: () ==> set of Partner
		GetPartners() ==
			return partners
		post RESULT = partners;

	/**
	 * Gets the sequence of rallies
	 * 
	 * @return seq of Rally
	 */			
	pure public GetSeries: () ==> seq of Rally
		GetSeries() ==
			return series
		post RESULT = series;

	/**
	 * Gets a team that matches the name given
	 * 
	 * @return Team
	 */		
	pure public GetTeamByName: (String) ==> Team
		GetTeamByName(tName) ==
			return teams(tName)
		pre tName in set dom teams;

	/**
	 * Gets a rally that matches the name given
	 * 
	 * @return Rally
	 */		
	pure public GetRallyByName: (String) ==> Rally
		GetRallyByName(rName) ==
			let i in set inds series be st series(i).GetName()=rName in return series(i)
		pre exists i in set inds series & series(i).GetName() = rName;

end Championship
